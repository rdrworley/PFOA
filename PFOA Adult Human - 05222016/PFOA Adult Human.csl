!Created by R. Worley on May 22, 2016.
!PBPK Model for PFOA exposure in human

PROGRAM

	INITIAL
			!Physiological Parameters		
			CONSTANT BW = 80	!body weight (kg); EPA Factors Handbook 2011
			CONSTANT htc = 0.44	!hematocrit; Davies and Morris 1994, Brown 1997
			CONSTANT Free = 0.02	!free fraction in plasma
			
			!Cardiac Output
			CONSTANT QCC = 12.5	!cardiac output (L/h/kg^0.75); Brown 1997, Forsyth 1968
			CONSTANT QLC = 0.25	!fraction cardiac output to liver; Brown 1997, Forsyth 1968
			CONSTANT QFC = 0.052	!fraction cardiac output to fat; Fisher 2000, Brown 1997
			CONSTANT QKC = 0.175	!fraction of cardiac output to kidney; Brown 1997, Forsyth 1968

			!Tissue Volumes
			CONSTANT VplasC = 0.0428	!fraction volume of plasma (L/kg BW); Davies 1993
			CONSTANT VfilC = 0.0004	!fraction volume of filtrate (L/kg BW);
			CONSTANT VLC = 0.0257	!fraction liver volume (L/kg BW); Brown 1997
			CONSTANT VKC = 0.0044	!fraction kidney volume (L/kg BW); Brown 1997
			CONSTANT VPTCC = 3.07e-5	!fraction volume of proximal tubule cells (L/kg BW)
			CONSTANT VFC = 0.2142	!fraction fat volume (L/kg BW); Brown 1997
			
			!Chemical Specific Parameters
			CONSTANT MW = 414.07
			
			!Partition Coefficients
			CONSTANT PL = 1.03	!liver:blood partition coefficient; Fabrega 2014
			CONSTANT PK = 1.17	!kidney:blood partition coefficient; Fabrega 2014
			CONSTANT PF = 0.47	!fat:blood partition coefficient; Fabrega 2014
			CONSTANT PR = 0.11	!rest of body:blood partition coefficient; Kudo 2007
			
			!Kidney Parameters
			CONSTANT GFRC = 0.106436	!glomerular filtration rate, male (L/hr/kg BW); Corley 2005
			CONSTANT Vmax_baso_invitro = 439.2	!Vmax of basolateral transporters (pmol/mg protein/min); average of in vitro measurements of OAT1 and OAT3; Nakagawa 2007 
			CONSTANT Km_baso = 20100	!Km of basolateral transporters (ug/L); average of in vitro measurements of OAT1 and OAT3; Nakagawa 2007
			CONSTANT Vmax_apical_invitro = 37400	!Vmax of apical transporters (pmol/mg protein/min); in vitro measurement for OAT4; Yang 2010
			CONSTANT Km_apical = 128500	!Km of apical transporters (ug/L); in vitro measurement for OAT4; Yang 2010
			CONSTANT RAFbaso = 0.01356	!relative activity factor, basolateral transporters; average of measurements of OAT1 and OAT3; Yamada, 2007
			CONSTANT RAFapical = 0.01356	!relative activity factor, apical transporters; fit to data
			
			CONSTANT PTC = 2.112e10	!nummber of proximal tubule cells (60 million PTC/g kidney)
			CONSTANT protein = 2.0e-6	!amount of protein in proximal tubule cells (mg protein/proximal tubule cell)

			CONSTANT kdif = 0.001	!difusion rate from proximal tubule cells (/h)
			CONSTANT keffluxC = 2.49	!rate of clearance of PFOA from proximal tubule cells into blood (/hr/kg BW^0.25); fit to data
			CONSTANT kurineC = 1.6	!rate of urinary elimination (/h/kg BW^0.25); fit to data

	END ! INITIAL


	DYNAMIC

		ALGORITHM IALG = 5
		NSTEPS    NSTP = 10
		MAXTERVAL MAXT = 1.0e9
		MINTERVAL MINT = 1.0e-9
		CINTERVAL CINT = 0.1

 		DERIVATIVE
 ! Scaled Parameters
		! Cardiac Output
		QC = QCC*(BW**0.75)*(1-Htc)	!cardiac output (L/h), adjusted for plasma flow
		QL = QLC*QC	!plasma flow to liver (L/h)
		QK = QKC*QC	!plasma flow to kidney (L/h)
		QF = QFC*QC	!plasma flow to fat (L/h)
		QR = QC - QL - QK - QF	!plasma flow to rest of body (L/h)
		Qbal = QC - (QL + QK + QF + QR)	!balance check of blood flows, should equal zero

		!Tissue Volumes
		VL = VLC*BW	!volume of liver (L)
		VK = VKC*BW	!volume of kidney (L)
		VKb = VK*0.16	!volume of blood in the kidney (L); fraction blood volume of kidney (0.16) Brown 1997
		VF = VFC*BW	!volume of fat (L)
		VR = (0.84*BW) - Vplas - VKb - VPTC - Vfil - VL - VK - VF	!volume of rest of body (L)
		Vplas = VplasC*BW	!volume of plasma (L)
		Vfil = VfilC*BW	!volume of filtrate (L)
		VPTC = VPTCC*BW	!volume of proximal tubule cells (L)

		!Kidney Parameters
		Vmax_basoC = (Vmax_baso_invitro*PTC*protein*MW*6.0e-5)*RAFbaso	!Vmax of basolateral transporters (ug/h/kg BW^0.75)
		Vmax_baso = Vmax_basoC*(BW**0.75)	!Vmax of basolateral transporters (ug/h)
		Vmax_apicalC = (Vmax_apical_invitro*PTC*protein*MW*6.0e-5) *RAFapical	!Vmax of apical transporters (ug/h/kg BW^0.75)
		Vmax_apical = Vmax_apicalC*(BW**0.75)	!Vmax of apical transporters (ug/h)
		kurine = kurinec*(BW**(-0.25))	!urinary elimination rate (/h)
		kefflux = kefflux*(BW**(-0.25))	!efflux clearance rate, from PTC to blood (/h)
		GFR = GFRC*BW	!glomerular filtration rate (L/h)

		!Exposure Parameters
		CONSTANT dwconc = 0.0	!drinking water concentration (ug/l)
		CONSTANT dwtotal = 1.2	!daily drinking water consumption (L); EPA Factors Handbook 2011
		CONSTANT drinks = 4	!total number of drinking episodes per day (drinks/day)
		CONSTANT drinktime = 0.25	!time spent drinking per episode (hours/drink)

		countdw = 0.0
		hourofday = 0.0
		dayofweek = 0.0
		day = t/24
		year = day/365

		DISCRETE Drinkint
		SCHEDULE DrinkON .AT. tstart
		END
		
		DISCRETE DrinkON

		INTERVAL C1 = 4.0 	!simulate drinking exposure every 4 hours
		If (hourofday .LT. 16.0) THEN
		drinkdose = (dwconc*dwtotal)/(drinktime*drinks)	!dose during each drinking water episode (ug/hour)
		SCHEDULE DrinkOFF .AT. T+drinktime	!turn off drinking water exposure after 15 minutes (drinktime)
		ENDIF

		hourofday = hourofday + 4	!increase hour of day count by four hours following each exposure

		IF(hourofday .EQ. 24.0) dayofweek = dayofweek + 1.0	!when hour of day count reaches 24, increase day of week count by 1
		IF(hourofday .EQ. 24.0) hourofday = 0.0 !when hour of day count reaches 24, reset hour of day count to zero
		IF (dayofweek .EQ. 7.0) dayofweek = 0.0 !when day of week count reaches 7, reset day of week count to zero
		END

		DISCRETE DrinkOFF
		drinkdose = 0.0	!set drinkdose to zero in between drinking water episodes
		END

		!MODEL EQUATIONS
		
		!Rest of Body
		RR = QR*(CA-CVR)*Free	!rate of change in the rest of body (ug/h)
		AR = integ(RR, 0.0)	!amount in the rest of body (ug)
		CR = AR/VR	!concentration in the rest of body (ug/L)
		CVR = CR/PR !concentraiton in the venous blood leaving the rest of body (ug/L)

		!Liver
		RL = QL*(CA-CVL)*Free	!rate of change in the liver (ug/h)
		AL = integ(RL, 0.0)	!amount in the liver (ug)
		CL = AL/VL	!concentration in th liver (ug/L)
		CVL = CL/PL	!concentration in venous blood leaving the liver (ug/L)

		!Fat
		RF = QF*(CA-CVF)*Free	!rate of change in the fat (ug/h)
		AF = integ(RF, 0.0)	!amount in the fat (ug)
		CF = AF/VF	!concentration in the fat (ug/L)
		CVF = CF/PF	!concentration in the venous blood leaving the fat (ug/L)

		!Kidney
			!Kidney Blood
			RKb = QK*(CA-CVK) - CA*GFR*Free - Rdif - RA_baso	!rate of change in the kidney blood (ug/h)
			AKb = integ(RKb, 0.0)	!amount in the kidney blood (ug)
			CKb = AKb/VKb	!concentraiton in the kidney blood (ug/L)
			CVK = CKb/PK	!concentration in the venous blood leaving the kidney (ug/L)

			Rdif = kdif(CKb - CPTC)	!rate of diffusion from the kidney blood into the PTC (ug/h)
			RA_baso = (Vmax_baso*CKb)/(Km_baso + CKb)	!rate of transport from the kidney blood into the PTC via the basolateral transporters (ug/h)
			A_baso = integ(RA_baso, 0.0)	!amount transported by the basolateral transporters (ug)

			!Proximal Tubule Cells
			RPTC = Rdif + RA_apical + RA_baso - RAefflux	!rate of change in the proximal tubule cells (ug/h)
			APTC = integ(RPTC, 0.0)	!amount in the proximal tubule cells (ug)
			CPTC = APTC/VPTC	!concentration in the proximal tubule cells (ug/L)
			
			RA_apical = (Vmax_apical*Cfil)/(Km_apical + Cfil)	!rate of transport from the filtrate to the proximal tubule cells via the apical transporters (ug/h)
			RAefflux = kefflux*APTC	!rate of efflux from proximal tubule cells to systemic circulation

		END ! DERIVATIVE

	
		! Add discrete events here as needed
		!  DISCRETE
		!  END

		! code that is executed once at each communication interval goes here

		CONSTANT TSTOP = 10.0
		TERMT (T .GE. TSTOP, 'checked on communication interval: REACHED TSTOP')

	END ! DYNAMIC


	TERMINAL
		! code that is executed once at the end of a simulation run goes here
	END ! TERMINAL

END ! PROGRAM
