PROGRAM PBPK Model for PFOA in Adult Male Rats. September 30, 2014

! Created by R. Worley based on previous model by A. Locissano
! Model includes compartments for GI Tract, Liver, Rest of Body(Tis), Kidney Blood, Proximal Tubule Cells, Filtrate, and Plasma.
! Exposure routes are oral (into gut compartment) and IV (into plasma compartment).
! Excretion pathways are feces and urine. 

	INITIAL

CONSTANT tstart = 0.0

	! Physiological Parameters
		CONSTANT BW = 0.3	!bodyweight (kg)

		! Cardiac Output and Bloodflow (as fraction of cardiac output)
		CONSTANT QCC = 14.0 !cardiac output in L/h/kg^0.75; Brown 1997		 
		CONSTANT QLC = 0.183 !fraction blood flow to liver; Brown 1997	
		CONSTANT QKC = 0.141 !fraction blood flow to kidney; Brown 1997.
		CONSTANT Htc = 0.46 !hematocrit for the rat; Davies 1993
		
		! Tissue Volumes 
		CONSTANT VplasC = 0.0312 !fraction vol. of plasma; Davies 1993
		CONSTANT VLC = 0.035 !fraction vol. of liver; Brown 1997
		CONSTANT VKC = 0.0084 !fraction vol. of kidney; Brown 1997
		CONSTANT VfilC = 8.4e-4	!fraction vol. of filtrate

		CONSTANT MKC = 0.0084	!fraction mass of kidney (percent of BW); Brown 1997

	! Chemical Specific Parameters
		!Free Fraction of Chemical in Plasma
		CONSTANT Free = 0.006 !free fraction in plasma (male); from Locissano, 2012 (fit to plasma and urine data)
		!CONSTANT Free = 0.045 !free fraction in plasma (female); from Locissano, 2012 (fit to plasma and urine data)
		
		!Kidney Transport Parameters
		CONSTANT Vmax_basolateral = .001 !Vmax of basolateral transporter (UNITS?) NNED VALUE, FIT 
		CONSTANT Km_basolateral = .001 !Km of basolateral transporter (UNITS?) NEED VALUE
		CONSTANT Vmax_apical = .003 !Vmax of apical transporter (UNITS?) NEED VALUE, FIT
		CONSTANT Km_apical = .003 !Km of apical transporter (UNITS?) NEED VALUE
				

		constant GFRC = 62.1	!glomerular filtration rate (L/hr/kg kidney) (male); Corley, 2005
		!constant GFRC = 41.04	!glomerular filtration rate (L/hr/kg kidney) (female); Corley, 2005

		!Partition Coefficients (from A. Locissano, from rat tissue data, Kudo et al, 2007)
		constant PL = 2.2 !liver:blood
		constant PK = 1.05 !kidney:blood
		constant PR = 0.11 !rest of body:blood
		
		!parameters for binding in liver (10/5/2014 - all set to zero per Locissano, 2012. Will need to reevaluate.)
		constant Kb = 1 !binding affinity constant (ug/L)
		constant Bmax = 1 !max. association rate constant (ug/h)
		constant koff= 1 !dissociation rate constant (/h)
 
		!rate constants (1/h)
		constant kdif = 0.001	!diffusion rate from proximal tubule cells (UNITS?)
		constant kabs = 31.3 !rate of absorption of chemical from GI into liver (/hr); from Locissano, 2012 (fit to plasma concentration data)
		constant kunabs = 0.0001 !rate of unabsorbed dose (male) to appear in feces (/h); from Locissano, 2012 (fit to feces data)
		!constant kunabs = 0.037 !rate of unabsorbed dose (female) to appear in feces (/h); from Locissano, 2012 (fit to feces data)
		
		CONSTANT keffluxc = 0.1	!rate of clearance of PFOA from proximal tubule cells into blood (/hr/kg^0.75) NEED VALUE, FIT
		constant kbilec = 0.35 !biliary elimination rate ((male); liver to feces storage (h/kg^-0.25); from Locissano, 2012 (fit to feces and plasma data)
		!constant kbilec = 0.25 !biliary elimination rate (female); liver to feces storage (h/kg^-0.25); from Locissano, 2012 (fit to feces and plasma data)
		constant kurinec = 0.1 !rate of urine elimination from urine storage (male) (/h/kg^-0.25); from Locissano, 2012 (fit to urine data)
		!constant kurinec = 0.32 !rate of urine elimination from urine storage (female) (/h/kg^-0.25); from Locissano, 2012 (fit to urine data)

	!Simulation (dosing) Parameters
	!Simulation parameters designed to replicate IV exposure data (male only) from Kudo et.al, 2007 and IV and oral exposure data (male and female) from Kemper et.al,2003.  
		!Oral Gavage Dosing
		!Kemper - Male and female rats administered a single oral gavage dose of 1, 5, or 25 mg/kg PFOA. Plasma collected (daily?) for 22 days in males, 5 days in females. Urine and feces
			!collected for 28 days in males, 7 days in females. NEED DATA.
		!Perkins - Male rats administered APFO at 0,1,10,30, or 100 ppm in diet for 13 weeks. Serum samples collected at 4, 7, and 13 weeks, and 8 weeks after
			!end of treatment. NEED DATA. 

		constant OdoseC = 10	!oral dose concentration(mg/kg)
		constant OGtime = 0.01	!length of oral gavage (hrs)
		
		!IV Dosing
		!Kudo - Male rats administered one IV dose (jugular) of 0.041 or 16.56 mg/kg PFOA, blood  (vena cava) and tissue samples collected 2 h after dosing.
		!Kudo - Male rats administered one IV dose (femoral) of 0.041 or 16.56 mg/kg PFOA, blood collected 9 times between 0 and 300 minutes. 
		!Kemper - Male and female rats administered one IV dose of 1 mg/kg PFOA. Blood samples collected from males 9 times in first 24 hours, at 24 hour intervals up to 8 days, 
			!and at 48 hour intervals up to 22 days. Blood samples collected from females 13 times over 4 days. 
		
		constant IVdoseC = 0	!IV dose concentration (mg/kg)
		constant IVTime = 0.01	!length of IV infusion (hrs)
	
	END ! INITIAL


	DYNAMIC

		ALGORITHM IALG = 5
		NSTEPS    NSTP = 10
		MAXTERVAL MAXT = 1.0e9
		MINTERVAL MINT = 1.0e-9
		CINTERVAL CINT = 0.01

 		DERIVATIVE
 	!Scaled Parameters
		!Cardiac output and blood flows
		QC = QCC*(BW**0.75)*(1-Htc)	!cardiac output in L/h; adjusted for plasma
		QK = (QKC*QC)	!plasma flow to kidney (L/h)
		QL = (QLC*QC)	!plasma flow to liver (L/h)
		QR = QC - QK - QL 	!plasma flow to rest of body (L/h)
		QBal = QC - (QK + QL + QR) !Balance check of blood flows; should equal zero

		GFR = GFRC*(MK/1000)	!glomerular filtration rate, scaled to mass of kidney(in kg)(L/h)

		!Tissue Volumes
		VPlas = VplasC*BW 	!volume of plasma (L) 
		VK = VKC*BW 	!volume of kidney (L)
		VKb = VK*0.16	!volume of blood in the kidney (L); fraction blood volume of kidney (0.16) from Brown, 1997
		Vfil = VfilC*BW	!volume of filtrate (L)
		VL = VLC*BW	!volume of liver (L)
		VR = (0.84*BW) - VPlas - VKb - VPTC - Vfil - VL - VK	!volume of remaining tissue (L); NOT SURE WHERE 0.84 COMES FROM
		VBal = (0.84*BW) - (VR + VL + VKb + VPTC + VFil + VPlas + VK)	!Balance check of tissue volumes; should equal zero 
		
		VPTCC = 6.0e7*2.25e-12 !vol. of proximal tubule cells (liters per gram kidney) (60 million PTC cells/gram kidney, 1 PTC = 2250 um3)
		MK = MKC*BW*1000	!mass of the kidney (g)
		VPTC = MK*VPTCC	!volume of proximal tubule cells (L)		

		kbile = kbilec*BW**(-0.25)	!biliary elimination; liver to feces storage (/h)
		kurine = kurinec*BW**(-0.25)	! urinary elimination, from filtrate (/h)
		kefflux = keffluxc*BW**(0.75)	!efflux clearance rate, from PTC to blood (/h)

	 !Dosing Equations
		!IV Dosing
		IVdose = IVdoseC*BW	!mg
		IVR = IVon*IVdose/IVTime	!IV dose rate (mg/hr)
		AIV = integ(IVR, 0.0)	!amount administered in IV (?) (mg)
		!IVon = Pulse(0, tstop, IVTime) !turn the IV dose on an off, need to modify to reflect simulated study
		IVon = RSW(T.GE.IVTime, 0.,1.)	!RSW switch to turn IV dose on. As written, will only work with a single, non-repeating IV dose. 
	
		!Oral Gavage Dosing
		Odose = OdoseC*BW	!oral dose (mg)
		OGR = OGon*Odose/OGtime	!Oral gavage rate (mg/hr)
		AOG = integ(OGR, 0.0)	!amount administered in oral gavage (?) (mg)
		OGon = RSW(T.GE.OGTime,0.,1.)	!RSW switch to turn on oral gavage. As written will only work with a single, non-repeating oral dose. s
		
 	!Model Equations
		!Rest of Body (Tis)
		RR = QR*(CA-CVR)*Free	!rate of change in rest of body (ug/h)
		AR = integ(RR, 0.0)	!amount in rest of body (ug)
		CR = AR/VR !concentration in rest of body (ug/L)
		CVR = CR/PR	!concentration in venous blood leaving the rest of the body

		!Kidney 
			!Kidney Blood (Kb)
			RKb = QK*(CA-CVK)*Free - CA*GFR*Free - kdif*CKb + kdif*CPTC - ((Vmax_basolateral*CKb)/(Km_basolateral + CKb)) + kefflux*CPTC !rate of change in kidney blood (ug/h).
			AKb = integ(RKb, 0.0) !amount in kidney blood (ug)
			CKb = AKb/VKb	!concentration in kidney blodd (ug/L) 
			CVK = CKb/PK	!concentration in venous blood leaving kidney (ug/L)

			!Proximal Tubule Cells (PTC)
			RPTC =  kdif*CKb - kdif*CPTC + ((Vmax_basolateral*CKb)/(Km_basolateral + CKb)) + (Vmax_apical*Cfil)/(Km_apical + Cfil) - kefflux*CPTC !rate of change in PTC(ug/h)
			APTC = integ(RPTC, 0.0)	!amount in proximal tubule cells (ug)
			CPTC = APTC/VPTC	!concentration in PTC (ug/L)

			!Filtrate (Fil)
			Rfil = CA*GFR*Free - ((Vmax_apical*Cfil)/(Km_apical + Cfil)) - Afil*kurine	!rate of change in filtrate (ug/h)
			Afil = integ(Rfil, 0.0)	!amount in filtrate (ug)
			Cfil = Afil/Vfil	!concentration in filtrate (ug/L)
			
			!Urinary elimination 
			Rurine = kurine*Afil	!rate of change in urine (ug/h)
			Aurine = integ(Rurine, 0.0)	!amount in urine (ug)

		!GI Tract (Absorption site of oral dose)
		RGI = -kabs*AGI + OGR - kunabs*AGI	!rate of change in the GI tract (ug/h) (accounts for loss to kidney (abs) and loss to feces (unabs), oral dose (OGR). Does not account for dietary exposure (ug/h)
		AGI = integ(RGI, 0.0)	!amount in GI tract (ug)
		
		!Liver
		RL = QL*(CA-CVL)*Free + kabs*AGI - kbile*AL - ((Bmax*CL)/(Kb + CL)) + koff*Abound	!rate of change in the liver (ug/h)
		AL = integ(RL, 0.0) !amount in liver (ug)
		CL = AL/VL	!concentration in the liver (ug/L)
		CVL = CL/PL	!concentration in the venous blood leaving the liver (ug/L)

		!Liver Binding
		Rbound = ((Bmax*CL)/(Kb + CL)) - koff*Abound	!rate of change of bound by liver proteins (ug/h)
		Abound = integ(Rbound, 0.0)	!amount bound by liver proteins (ug)
				
		!Total Amount in Liver
		ALtotal = AL + Abound	!total amount (bound and unbound) in liver (ug)
		CLtotal = ALtotal/VL	!total concentration in liver (ug/L) [to be used to compare to measured liver concentrations from rat studies]
		
		!Feces compartment
		Rfeces = kunabs*AGI + kbile*AL !rate of change in the feces compartment (ug/h)
		Afeces = integ(Rfeces, 0.0)	!amount in the feces compartment (ug)
		
		!Plasma compartment
		RPlas = (QR*CVR*Free) + (QK*CVK*Free) + (QL*CVL*Free) - (QC*CA*Free) + IVR 	!rate of change in the plasma (ug/h)
		Aplas = integ(RPlas, 0.0)
		CA = APlas/VPlas
		
		!Mass Balance Check
		Atissue = APlas + AR + AKb + AFil + ALtotal + AGI !+ APTC 	!sum of mass in all compartments (ug)
		Aloss = Aurine + Afeces 	!sum of mass lost through urinary and fecal excretion (ug)
		Atotal = Atissue + Aloss 	!total mass; should equal total dose

	
		END ! DERIVATIVE

		! Add discrete events here as needed
		!  DISCRETE
		!  END

		! code that is executed once at each communication interval goes here

		CONSTANT TSTOP = 24.0 !hours
		TERMT (T .GE. TSTOP, 'checked on communication interval: REACHED TSTOP')

	END ! DYNAMIC


	TERMINAL
		! code that is executed once at the end of a simulation run goes here
	END ! TERMINAL

END ! PROGRAM
