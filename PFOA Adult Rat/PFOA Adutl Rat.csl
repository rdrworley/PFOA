PROGRAM PBPK Model for PFOA in Adult Male Rats. 
! Created by R. Worley based on previous model by A. Locissano
! Model includes compartments for GI Tract, Liver, Rest of Body(Tis), Kidney Blood, Proximal Tubule Cells, Filtrate, and Plasma.
! Exposure routes are oral (into gut compartment) and IV (into plasma compartment).
! Excretion pathways are feces and urine. 

	INITIAL

CONSTANT tstart = 0.0

	! Physiological Parameters
		CONSTANT BW = 0.3	!bodyweight (kg)
		CONSTANT MKC = 0.0084	!fraction mass of kidney (percent of BW); Brown 1997
		CONSTANT MLC = 0.034	!fraction mass of liver (percent of BW); Brown 1997

		! Cardiac Output and Bloodflow (as fraction of cardiac output)
		CONSTANT QCC = 14.0 !cardiac output in L/h/kg^0.75; Brown 1997		 
		CONSTANT QLC = 0.183 !fraction blood flow to liver; Brown 1997	
		CONSTANT QKC = 0.141 !fraction blood flow to kidney; Brown 1997.
		CONSTANT Htc = 0.46 !hematocrit for the rat; Davies 1993
		
		! Tissue Volumes 
		CONSTANT VplasC = 0.0312 !fraction vol. of plasma (L/kg BW); Davies 1993
		CONSTANT VLC = 0.035 !fraction vol. of liver (L/kg BW); Brown 1997
		CONSTANT VKC = 0.0084 !fraction vol. of kidney (L/kg BW); Brown 1997
		CONSTANT VfilC = 8.4e-4	!fraction vol. of filtrate (L/kg BW)
		CONSTANT VPTCC = 1.35e-4 !vol. of proximal tubule cells (L/g kidney) (60 million PTC cells/gram kidney, 1 PTC = 2250 um3)
	
	! Chemical Specific Parameters
		CONSTANT MW = 414.07	!PFOA molecular mass (g/mol)
		!Free Fraction of Chemical in Plasma
		CONSTANT Free = 0.006 !free fraction in plasma (male); from Locissano, 2012 (fit to plasma and urine data)
		!CONSTANT Free = 0.045 !free fraction in plasma (female); from Locissano, 2012 (fit to plasma and urine data)
		
		!Kidney Transport Parameters
		CONSTANT Vmax_baso_invitro = 393.45 !Vmax of basolateral transporter (pmol/mg protein/min); averaged in vitro value of OAT1 and OAT3 from Nakagawa, 2007
		CONSTANT Km_baso = 37.7 !Km of basolateral transporter (ug/mL) Average of OAT1 and OAT3 from Nakagawa et. al, 2007
		CONSTANT Vmax_apical_invitro = 18861.8 !Vmax of apical transporter (pmol/mg protein/min); averaged in vitro value of OAT4 and URAT1 from Nakagawa, 2007
		CONSTANT Km_apical = 141 !Km of apical transporter (ug/mL), Average of OAT4 and URAT1 from Nakagawa et. al, 2007
		CONSTANT RAFbaso = 0.01356	!relative activity factor, basolateral transporters (averaged from OAT1 and OAT3 in Yamada, 2007)
		CONSTANT RAFapi = 0.01356	!relative activity factor, apical transporters (NEED VALUE - current value is copied from RAF for baso)		
		CONSTANT protein = 2.0e-6	!amount of protein in proximal tubule cells (mg protein/proximal tubule cell)

		constant GFRC = 62.1	!glomerular filtration rate (L/hr/kg kidney) (male); Corley, 2005
		!constant GFRC = 41.04	!glomerular filtration rate (L/hr/kg kidney) (female); Corley, 2005

		!Partition Coefficients (from A. Locissano, from rat tissue data, Kudo et al, 2007)
		constant PL = 2.2 !liver:blood
		constant PK = 1.05 !kidney:blood
		constant PR = 0.11 !rest of body:blood
		
		!parameters for binding in liver (10/5/2014 - all set to zero per Locissano, 2012. Will need to reevaluate.)
		constant Kb = 1 !binding affinity constant (ug/mL); 
		constant Bmax = 1 !max. association rate constant (ug/h); 
		constant koff= 1 !dissociation rate constant (/h); 
 
		!rate constants (1/h)
		constant kdif = 0.001	!diffusion rate from proximal tubule cells (UNITS?)
		constant kabs = 0.3 !rate of absorption of chemical from small intestine to liver (/hr); fit to time-to-peak
		constant kunabs = 0.0001 !rate of unabsorbed dose (male) to appear in feces (/h); from Locissano, 2012 (fit to feces data)
		CONSTANT GEC = 0.54 !gastric emptying time (kg^0.25/h); from Yang, 2013
		CONSTANT k0C = 1.0e-33	!rate of uptake from the stomach into the liver (1/h/BW^-0.25)
		
		CONSTANT keffluxc = 10	!rate of clearance of PFOA from proximal tubule cells into blood (/hr/kg^0.75) NEED VALUE, FIT
		constant kbilec = 0.0075 !biliary elimination rate ((male); liver to feces storage (/h/kg^-0.25); fit to serum data
		!constant kbilec = 0.25 !biliary elimination rate (female); liver to feces storage (/h/kg^-0.25); from Locissano, 2012 (fit to feces and plasma data)
		constant kurinec = 0.1 !rate of urine elimination from urine storage (male) (/h/kg^-0.25); from Locissano, 2012 (fit to urine data)
		!constant kurinec = 0.32 !rate of urine elimination from urine storage (female) (/h/kg^-0.25); from Locissano, 2012 (fit to urine data)

	!Simulation (dosing) Parameters
	!Simulation parameters designed to replicate IV exposure data (male only) from Kudo et.al, 2007 and IV and oral exposure data (male and female) from Kemper et.al,2003.  
		!Oral Gavage Dosing
		!Kemper - Male and female rats administered a single oral gavage dose of 0.1, 1, 5, or 25 mg/kg PFOA. Plasma collected (daily?) for 22 days in males, 5 days in females. Urine and feces
			!collected for 28 days in males, 7 days in females. NEED DATA.
		!Perkins - Male rats administered APFO at 0,1,10,30, or 100 ppm in diet for 13 weeks. Serum samples collected at 4, 7, and 13 weeks, and 8 weeks after
			!end of treatment. NEED DATA. 

		constant OdoseC = 1.0e-33	!oral dose concentration(mg/kg BW)
		constant OGtime = 0.01	!length of oral gavage (hrs)
		
		!IV Dosing
		!Kudo - Male rats administered one IV dose (jugular) of 0.041 or 16.56 mg/kg PFOA, blood  (vena cava) and tissue samples collected 2 h after dosing.
		!Kudo - Male rats administered one IV dose (femoral) of 0.041 or 16.56 mg/kg PFOA, blood collected 9 times between 0 and 300 minutes. 
		!Kemper - Male and female rats administered one IV dose of 1 mg/kg PFOA. Blood samples collected from males 9 times in first 24 hours, at 24 hour intervals up to 8 days, 
			!and at 48 hour intervals up to 22 days. Blood samples collected from females 13 times over 4 days. 
		
		constant IVdoseC = 1.0	!IV dose concentration (mg/kg)
		constant IVTime = 0.01	!length of IV infusion (hrs)
	
	END ! INITIAL


	DYNAMIC

		ALGORITHM IALG = 2
		NSTEPS    NSTP = 10
		MAXTERVAL MAXT = 1.0e9
		MINTERVAL MINT = 1.0e-9
		CINTERVAL CINT = 0.1

 		DERIVATIVE
 	!Scaled Parameters
		!Cardiac output and blood flows
		QC = QCC*(BW**0.75)*(1-Htc)	!cardiac output in L/h; adjusted for plasma
		QK = (QKC*QC)	!plasma flow to kidney (L/h)
		QL = (QLC*QC)	!plasma flow to liver (L/h)
		QR = QC - QK - QL 	!plasma flow to rest of body (L/h)
		QBal = QC - (QK + QL + QR) !Balance check of blood flows; should equal zero

		!Tissue Volumes
		VPlas = VplasC*BW 	!volume of plasma (L) 
		VK = VKC*BW 	!volume of kidney (L)
		VKb = VK*0.16	!volume of blood in the kidney (L); fraction blood volume of kidney (0.16) from Brown, 1997
		Vfil = VfilC*BW	!volume of filtrate (L)
		VL = VLC*BW	!volume of liver (L)
		VR = (0.84*BW) - VPlas - VKb - VPTC - Vfil - VL - VK	!volume of remaining tissue (L); NOT SURE WHERE 0.84 COMES FROM
		VBal = (0.84*BW) - (VR + VL + VKb + VPTC + VFil + VPlas + VK)	!Balance check of tissue volumes; should equal zero 
		ML = MLC*BW*1000	!mass of the liver (g)

		!Kidney Parameters
		MK = MKC*BW*1000	!mass of the kidney (g)
		PTC = MK*6e7	!number of PTC (60 million PTC/gram kidney)
		VPTC = MK*VPTCC	!volume of proximal tubule cells (L)	
		MPTC = VPTC*1000 !mass of the proximal tubule cells (g) (assuming density 1 kg/L)	
		Vmax_basoC = (Vmax_baso_invitro*RAFbaso*PTC*protein*60*(MW/1e12)*1000)!Vmax of basolateral transporters (mg/h/kg BW ^0.75)
		Vmax_apicalC = (Vmax_apical_invitro*RAFapi*PTC*protein*60*(MW/1e12)*1000) !Vmax of basolateral transporters (mg/h/kg BW ^0.75)
		Vmax_baso = Vmax_basoC*BW**0.75	!(mg/h)
		VMax_apical = Vmax_apicalC*BW**0.75	!(mg/h)
		kbile = kbilec*BW**(-0.25)	!biliary elimination; liver to feces storage (/h)
		kurine = kurinec*BW**(-0.25)	! urinary elimination, from filtrate (/h)
		kefflux = keffluxc*BW**(0.75)	!efflux clearance rate, from PTC to blood (/h)
		GFR = GFRC*(MK/1000)	!glomerular filtration rate, scaled to mass of kidney(in kg)(L/h)

		!GI Tract Parameters
		GE = GEC/BW**0.25	!gastric emptying time (/h)
		k0 = K0C/BW**0.25 	!rate of uptake from the stomach into the liver (/h)

	 !Dosing Equations
		!IV Dosing
		IVdose = IVdoseC*BW	!mg
		IVR = IVon*IVdose/IVTime	!IV dose rate (mg/hr)
		AIV = integ(IVR, 0.0)	!amount administered in IV (?) (mg)
		!IVon = Pulse(0, tstop, IVTime) !turn the IV dose on an off, need to modify to reflect simulated study
		IVon = RSW(T.GE.IVTime, 0.,1.)	!RSW switch to turn IV dose on. As written, will only work with a single, non-repeating IV dose. 
	
		!Oral Gavage Dosing
		Odose = OdoseC*BW	!oral dose (mg)
		OGR = OGon*Odose/OGtime	!Oral gavage rate (mg/hr)
		AOG = integ(OGR, 0.0)	!amount administered in oral gavage (mg)
		OGon = RSW(T.GE.OGTime,0.,1.)	!RSW switch to turn on oral gavage. As written will only work with a single, non-repeating oral dose. 
		
		!Model Equations
		!Rest of Body (Tis)
		RR = QR*(CA-CVR)*Free	!rate of change in rest of body (mg/h)
		AR = integ(RR, 0.0)	!amount in rest of body (mg)
		CR = AR/VR !concentration in rest of body (mg/L)
		CVR = CR/PR	!concentration in venous blood leaving the rest of the body (mg/L)
		
		!Kidney 
			!Kidney Blood (Kb)
			RKb = QK*(CA-CVK)*Free - CA*GFR*Free - Rdif - RA_baso   !rate of change in kidney blood (mg/h).
			AKb = integ(RKb, 0.0) !amount in kidney blood (mg)
			CKb = AKb/VKb	!concentration in kidney blodd (mg/L) 
			CVK = CKb !/PK	!concentration in venous blood leaving kidney (mg/L)

			Rdif = Kdif*(CKb - CPTC)	!rate of diffusion from into the PTC (mg/hr)
			Adif = integ(Rdif, 0.)	!amount diffused into the PTC (mg)

			RA_baso = (Vmax_baso*CKb)/(Km_baso + CKb)
			A_baso = integ(RA_baso, 0.)

			!Proximal Tubule Cells (PTC)
			RPTC =  Rdif + RA_apical + RA_baso - RAefflux !rate of change in PTC(mg/h)
			APTC = integ(RPTC, 0.0)	!amount in proximal tubule cells (mg)
			CPTC = APTC/VPTC	!concentration in PTC (mg/L)

			RA_apical = (Vmax_apical*Cfil)/(Km_apical + Cfil)
			A_apical = integ(RA_apical, 0.)

			RAefflux = kefflux*APTC
			Aefflux = integ(RAefflux, 0.)

			!Filtrate (Fil)
			Rfil = CA*GFR*Free - RA_apical - Afil*kurine	!rate of change in filtrate (mg/h)
			Afil = integ(Rfil, 0.0)	!amount in filtrate (mg)
			Cfil = Afil/Vfil	!concentration in filtrate (mg/L)
			
			!Urinary elimination 
			Rurine = kurine*Afil	!rate of change in urine (mg/h)
			Aurine = integ(Rurine, 0.0)	!amount in urine (mg)

			percentOD_in_urine = (Aurine/Odose)*100

		!GI Tract (Absorption site of oral dose)
			!Stomach
			RST= OGR - k0*AST - GE*AST	!rate of change in the stomach (mg/h)
			AST = integ(RST, 0.)	!amount in the stomach (mg)

			RabsST = k0*AST	!rate of absorption in the stomach
			AabsST = integ(RabsST, 0.)	!amount absorbed in the stomach

			!Small Intestine
			RSI = GE*AST - kabs*ASI - kunabs*ASI	!rate of change in the small intestine (mg/hr)
			ASI = integ(RSI, 0.)	!amount in the small intestine (mg)

			RabsSI = kabs*ASI	!rate of absorption inthe small intestine (mg/hr)
			AabsSI = integ(RabsSI, 0.)	!amount absorbed in the small intestine (mg)

			total_oral_uptake = AabsSI + AabsST	!total oral uptake in the GI tract (mg) 

			!Feces compartment
			Rfeces = kbile*AL + kunabs*ASI !rate of change in the feces compartment (mg/h)
			Afeces = integ(Rfeces, 0.0)	!amount in the feces compartment (mg)
			percentOD_in_feces = (Afeces/Odose)*100	!percent of the oral dose in the feces (%)

		!Liver
		RL = QL*(CA-CVL)*Free - kbile*AL + kabs*ASI + k0*AST !- ((Bmax*CL)/(Kb + CL)) + koff*Abound	!rate of change in the liver (mg/h)
		AL = integ(RL, 0.0) !amount in liver (mg)
		CL = AL/VL	!concentration in the liver (mg/L)
		CVL = CL/PL	!concentration in the venous blood leaving the liver (mg/L)
		
		Abile = kbile*AL
		amount_per_gram_liver = (AL/ML)*1000	!amount of PFOA in liver per gram liver (ug/g)

		!Liver Binding
		Rbound = ((Bmax*CL)/(Kb + CL)) - koff*Abound	!rate of change of bound by liver proteins (mg/h)
		Abound = integ(Rbound, 0.0)	!amount bound by liver proteins (mg)
				
		!Total Amount in Liver
		ALtotal = AL + Abound	!total amount (bound and unbound) in liver (mg)
		CLtotal = ALtotal/VL	!total concentration in liver (mg/L) [to be used to compare to measured liver concentrations from rat studies]
		
		!Plasma compartment
		RPlas_free = (QR*CVR*Free) + (QK*CVK*Free) + (QL*CVL*Free) - (QC*CA*Free) + IVR + RAefflux  !rate of change in the plasma (mg/h) OLD VERSION
		Aplas_free = integ(RPlas_free, 0.0)	!amount in the plasma (mg)
		CA_free = APlas_free/VPlas		!concentration in plasma (mg)
		CA = CA_free/Free	!concentration of total PFOA in plasma (mg/L)

		!Mass Balance Check
		Atissue = APlas_free + AR + AKb + AFil + APTC + AL + AST + ASI 	!sum of mass in all compartments (mg)
		Aloss = Aurine + Afeces 	!sum of mass lost through urinary and fecal excretion (mg)
		Atotal = Atissue + Aloss 	!total mass; should equal total dose
	
		END ! DERIVATIVE

		! Add discrete events here as needed
		!  DISCRETE
		!  END

		! code that is executed once at each communication interval goes here

		CONSTANT TSTOP = 24.0 !hours
		TERMT (T .GE. TSTOP, 'checked on communication interval: REACHED TSTOP')

	END ! DYNAMIC


	TERMINAL
		! code that is executed once at the end of a simulation run goes here
	END ! TERMINAL

END ! PROGRAM