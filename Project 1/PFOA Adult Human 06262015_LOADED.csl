PROGRAM PBPK Model for PFOA in Adult Humans. 
! Created by R. Worley scaled from PFOA Adult Rat Model
! Started June 26, 2015
! Model includes compartments for GI Tract, Liver, Rest of Body(Tis), Kidney Blood, Proximal Tubule Cells, Filtrate, and Plasma.
! Exposure route is oral ingestion (into gut compartment)
! Excretion pathways are feces and urine. 
! Units of concentration in ug/L. 

	INITIAL

CONSTANT tstart = 0.0
		
	! Physiological Parameters
		CONSTANT BW = 80.0	!bodyweight (kg); EPA Factors Handbook, 2011
		
		! Cardiac Output and Bloodflow (as fraction of cardiac output)
		CONSTANT QCC = 12.5 !cardiac output in L/h/kg^0.75; Brown 1997, Forsyth 1968		 
		CONSTANT QLC = 0.25 !fraction blood flow to liver; Brown 1997, Fisher 2000	
		CONSTANT QKC = 0.175 !fraction blood flow to kidney; Brown 1997, Forsyth 1968
		CONSTANT Htc = 0.44 !hematocrit for the rat; Davies and Morris 1993, Brown 1997
		
		! Tissue Volumes 
		CONSTANT VplasC = 0.0428 !fraction vol. of plasma (L/kg BW); Davies 1993
		CONSTANT VLC = 0.026 !fraction vol. of liver (L/kg BW); Brown 1997
		CONSTANT VKC = 0.004 !fraction vol. of kidney (L/kg BW); Brown 1997
		CONSTANT VfilC = 0.0004	!fraction vol. of filtrate (L/kg BW)
		CONSTANT VPTCC = 1.35e-4 !vol. of proximal tubule cells (L/g kidney) (60 million PTC cells/gram kidney, 1 PTC = 2250 um3) CHECK
	
	! Chemical Specific Parameters
		CONSTANT MW = 414.07	!PFOA molecular mass (g/mol)
		CONSTANT Free = 0.02 !free fraction in plasma (initial value from Loccisano, 2011)

		!Kidney Transport Parameters BASO PARAMETERS UPDATED FOR HUMAN, STILL NEED APICAL Vmax (current is OAT4 only)
		CONSTANT Vmax_baso_invitro = 439.2 !Vmax of basolateral transporter (pmol/mg protein/min); averaged in vitro value of OAT1 and OAT3 from Nakagawa, 2007
		CONSTANT Km_baso = 20100.0 !Km of basolateral transporter (ug/L) Average of OAT1 and OAT3 from Nakagawa et. al, 2007
		CONSTANT Vmax_apical_invitro = 37400.0 !Vmax of apical transporter (pmol/mg protein/min); invitro value for OAT4 from Yang, 2010. 
		CONSTANT Km_apical = 77500.0 !Km of apical transporter (ug/L), averaged in vitro value for OAT4 and URAT1 from Yang, 2010. 
		CONSTANT RAFbaso = 1.0	!relative activity factor, basolateral transporters (averaged from OAT1 and OAT3 in Yamada, 2007)
		CONSTANT RAFapi = 0.007	!relative activity factor, apical transporters (fit to data)	
		CONSTANT protein = 2.0e-6	!amount of protein in proximal tubule cells (mg protein/proximal tubule cell)

		CONSTANT GFRC = 24.19	!glomerular filtration rate (L/hr/kg kidney) (male); Corley, 2005
		!CONSTANT GFRC = 27.28	!glomerular filtration rate (L/hr/kg kidney) (female); Corley, 2005

		!Partition Coefficients 
		CONSTANT PL = 1.03 !liver:blood (from human cadaver data, Fabrega, 2014)
		CONSTANT PK = 1.17 !kidney:blood (from human cadaver data, Fabrega, 2014)
		CONSTANT PR = 0.11 !rest of body:blood (from rat tissue data, Kudo, 2007)
		
		!rate constants 
		CONSTANT kdif = 0.001	!diffusion rate from proximal tubule cells (L/h)
		CONSTANT kabsc = 2.12	!rate of absorption of chemical from small intestine to liver (1/(h*BW^0.25))(fit to data)
		CONSTANT kunabsc = 7.06e-5	!rate of unabsorbed dose to appear in feces (1/(h*BW^0.25)) (fit to data)
		CONSTANT GEC = 3.5 !gastric emptying time (1/(h*BW^0.25)); from Yang, 2014
		CONSTANT k0C = 1.0	!rate of uptake from the stomach into the liver (1/(h*BW^0.25))
		
		CONSTANT keffluxc = 0.03	!rate of clearance of PFOA from proximal tubule cells into blood (1/(h*BW^0.25))); fit to data
		CONSTANT kbilec = 0.004 !biliary elimination rate ((male); liver to feces storage (1/(h*BW^0.25)); fit to data
		CONSTANT kurinec = 1.2 !rate of urine elimination from urine storage (male) (1/(h*BW^0.25))(fit to data)
		CONSTANT kvoid = 0.06974	!daily urine volume rate (L/hr); Van Haarst, 2004

		! Exposure Parameters
		countdw = 0.0
		hourofday = 0.0
		dayofweek = 0.0 

	END ! INITIAL

	DYNAMIC

		ALGORITHM IALG = 2
		NSTEPS    NSTP = 10
		MAXTERVAL MAXT = 1.0e9
		MINTERVAL MINT = 1.0e-9
		CINTERVAL CINT = 10.0

 		DERIVATIVE
 	!Scaled Parameters
		!Cardiac output and blood flows
		QC = QCC*(BW**0.75)*(1-Htc)	!cardiac output in L/h; adjusted for plasma
		QK = (QKC*QC)	!plasma flow to kidney (L/h)
		QL = (QLC*QC)	!plasma flow to liver (L/h)
		QR = QC - QK - QL 	!plasma flow to rest of body (L/h)
		QBal = QC - (QK + QL + QR) !Balance check of blood flows; should equal zero

		!Tissue Volumes
		VPlas = VplasC*BW 	!volume of plasma (L) 
		VK = VKC*BW 	!volume of kidney (L)
		MK = VK*1.0*1000	!mass of kidney (g); based on density of kidney = 1.0 g/mL
		VKb = VK*0.16	!volume of blood in the kidney (L); fraction blood volume of kidney (0.16) from Brown, 1997
		Vfil = VfilC*BW	!volume of filtrate (L)
		VL = VLC*BW	!volume of liver (L)
		ML = VL*1.05*1000	!mass of liver (g); based on density of liver = 1.05 g/mL, Overmeyer 1987
		VR = (0.93*BW) - VPlas - VPTC - Vfil - VL	!volume of remaining tissue (L)[Note: VKb not included as already accounted for in VPlas]; 
		VBal = (0.93*BW) - (VR + VL + VPTC + VFil + VPlas)	!Balance check of tissue volumes; should equal zero 

		!Kidney Parameters
		PTC =VKC*1000*6e7	!number of PTC (cells/kg BW) (based on 60 million PTC/gram kidney)
		VPTC = VK*1000*VPTCC	!volume of proximal tubule cells (L)	
		MPTC = VPTC*1000 !mass of the proximal tubule cells (g) (assuming density 1 kg/L)	
		Vmax_basoC = (Vmax_baso_invitro*RAFbaso*PTC*protein*60*(MW/1e12)*1000000)!Vmax of basolateral transporters (ug/h/kg BW ^0.75)
		Vmax_apicalC = (Vmax_apical_invitro*RAFapi*PTC*protein*60*(MW/1e12)*1000000) !Vmax of basolateral transporters (ug/h/kg BW ^0.75)
		Vmax_baso = Vmax_basoC*BW**0.75	!(ug/h)
		VMax_apical = Vmax_apicalC*BW**0.75	!(ug/h)
		kbile = kbilec*BW**(-0.25)	!biliary elimination; liver to feces storage (/h)
		kurine = kurinec*BW**(-0.25)	! urinary elimination, from filtrate (/h)
		kefflux = keffluxc*BW**(-0.25)	!efflux clearance rate, from PTC to blood (/h)
		GFR = GFRC*VK	!glomerular filtration rate, scaled to mass of kidney(in kg)(L/h)

		!GI Tract Parameters
		kabs = kabsc*BW**(-0.25)	!rate of absorption of chemical from small intestine to liver (/h)
		kunabs = kunabsc*BW**(-0.25)	!rate of unabsorbed dose to appear in feces (/h)
		GE = GEC/BW**0.25	!gastric emptying time (/h)
		k0 = K0C/BW**0.25 	!rate of uptake from the stomach into the liver (/h)

! Exposure Parameters
	day = t/24
	year = day/365
	
	!Drinking Water
	CONSTANT backgrounddw = 0.01 ! Drinking water concentration (ug/L or ppb)
	CONSTANT exposeddw = 3.55 !Contaminated drinking water concentration (ug/L or ppb)
	CONSTANT dwtotal = 1.3 ! daily drinking water consumption (L), from the EPA Factors Handbook (2011)
	CONSTANT drinks = 4.0 !Total drinks per day (drink)
	CONSTANT tlendw = 0.25 !Length of time spent drinking each drinking event (hrs/drink)
	CONSTANT tbackground = 0.0	!duration of exposure to background dw concentration (hrs)

	!Incidental Ingestion
	CONSTANT ingest = 0.01 	!incidental ingestion rate (ug/hr)

!Drinking water exposure
DISCRETE Drinkint
SCHEDULE DrinkON .AT. tstart
END

DISCRETE DrinkON
IF (T.LT.tbackground) THEN !if time is less than tbackground, 
INTERVAL C2 = 4.0 !simulate drinking exposure every four hours
IF (hourofday .LT. 16.0) THEN !if hour of day is less than 16 (8pm)
drinkdose = ((backgrounddw/tlendw)*(dwtotal/drinks)) !simulate drinking water (background concentration)exposure at set interval(ug/hr)
countdw = countdw + 1 !increase drinking episode count by 1 following exposure
SCHEDULE DrinkOFF .AT. T+tlendw !turn off drinking water exposure after 15 minutes (tlen)
ENDIF

hourofday = hourofday + 4.0 !increase hour of day count by four hours following each exposure

IF(hourofday .EQ. 24.0) dayofweek = dayofweek + 1.0 !when hour of day count reaches 24, increase day of week count by 1
IF(hourofday .EQ. 24.0) hourofday = 0.0 !when hour of day count reaches 24, reset hour of day count to zero
IF (dayofweek .EQ. 7.0) dayofweek = 0.0 !when day of week count reaches 7, reset day of week count to zero
ENDIF

END
adrinkdose = integ(drinkdose, 0.)

DISCRETE DrinkON
IF (T.GT.tbackground) THEN !if time is greater than tbackground, 
INTERVAL C2 = 4.0 !simulate drinking exposure every four hours
IF (hourofday .LT. 16.0) THEN !if hour of day is less than 16 (8pm)
drinkdose = ((exposeddw/tlendw)*(dwtotal/drinks)) !simulate drinking water (exposed concentration)exposure at set interval(ug/hr)
countdw = countdw + 1.0 !increase drinking episode count by 1 following exposure
SCHEDULE DrinkOFF .AT. T+tlendw !turn off drinking water exposure after 15 minutes (tlen)
ENDIF

hourofday = hourofday + 4.0 !increase hour of day count by four hours following each exposure

IF(hourofday .EQ. 24.0) dayofweek = dayofweek + 1.0 !when hour of day count reaches 24, increase day of week count by 1
IF(hourofday .EQ. 24.0) hourofday = 0.0 !when hour of day count reaches 24, reset hour of day count to zero
IF (dayofweek .EQ. 7.0) dayofweek = 0.0 !when day of week count reaches 7, reset day of week count to zero
ENDIF

END
adrinkdose2 = integ(drinkdose, 0.0)

DISCRETE DrinkOFF 
Drinkdose = 0.0 !set drink dose to zero during non-exposure periods
END	
	
!Model Equations
		!Rest of Body (Tis)
		RR = QR*(CA-CVR)*Free	!rate of change in rest of body (ug/h)
		AR = integ(RR, 134.447)	!amount in rest of body (ug)
		CR = AR/VR !concentration in rest of body (ug/L)
		CVR = CR/PR	!concentration in venous blood leaving the rest of the body (ug/L)
		
		!Kidney 
			!Kidney Blood (Kb)
			RKb = QK*(CA-CVK)*Free - CA*GFR*Free - Rdif - RA_baso   !rate of change in kidney blood (ug/h).
			AKb = integ(RKb, 0.103344 ) !amount in kidney blood (ug)
			CKb = AKb/VKb	!concentration in kidney blodd (ug/L) 
			CVK = CKb !/PK	!concentration in venous blood leaving kidney (ug/L)

			RCl = CA*GFR*Free	!rate of clearance via glomerular filtration (ug/h)
			ACl = integ(RCl, 788877.0)		!amount moved via glomerular filtration (ug)
	
			Rdif = Kdif*(CKb - CPTC)	!rate of diffusion from into the PTC (ug/hr)
			Adif = integ(Rdif, -1.83679e+006)	!amount diffused into the PTC (ug/L)

			RA_baso = (Vmax_baso*CKb)/(Km_baso + CKb)
			A_baso = integ(RA_baso, 3.93001e+006)

			!Proximal Tubule Cells (PTC)
			RPTC =  Rdif + RA_apical + RA_baso - RAefflux !rate of change in PTC(ug/h)
			APTC = integ(RPTC, 307.759)	!amount in proximal tubule cells (ug)
			CPTC = APTC/VPTC	!concentration in PTC (ug/L)

			RA_apical = (Vmax_apical*Cfil)/(Km_apical + Cfil)
			A_apical = integ(RA_apical, 781970.0)

			RAefflux = kefflux*APTC
			Aefflux = integ(RAefflux, 2.87488e+006)

			!Filtrate (Fil)
			Rfil = CA*GFR*Free - RA_apical - Afil*kurine	!rate of change in filtrate (ug/h)
			Afil = integ(Rfil,1.16428)	!amount in filtrate (ug)
			Cfil = Afil/Vfil	!concentration in filtrate (ug/L)
			
			!Urinary elimination 
			Rurine = kurine*Afil	!rate of change in urine (ug/h)
			Aurine = integ(Rurine, 6905.49)	!amount in urine (ug)
			Curine = Rurine/kvoid	!concentration in urine (ug/L)
		
		!GI Tract (Absorption site of oral dose)
			!Stomach
			RST= ingest + drinkdose - k0*AST - GE*AST	!rate of change in the stomach (ug/h)
			AST = integ(RST, 0.00311232)	!amount in the stomach (ug)

			RabsST = k0*AST	!rate of absorption in the stomach (ug/h)
			AabsST = integ(RabsST, 1641.89)	!amount absorbed in the stomach (ug)

			!Small Intestine
			RSI = GE*AST - kabs*ASI - kunabs*ASI	!rate of change in the small intestine (ug/hr)
			ASI = integ(RSI, 0.0169123)	!amount in the small intestine (ug)

			RabsSI = kabs*ASI	!rate of absorption inthe small intestine (ug/hr)
			AabsSI = integ(RabsSI, 5746.41)	!amount absorbed in the small intestine (ug)

			total_oral_uptake = AabsSI + AabsST	!total oral uptake in the GI tract (ug) 

			!Feces compartment
			Rfeces = kbile*AL + kunabs*ASI !rate of change in the feces compartment (ug/h)
			Afeces = integ(Rfeces,0.191366)	!amount in the feces compartment (ug)

		!Liver
		RL = QL*(CA-CVL)*Free - kbile*AL + kabs*ASI + k0*AST !rate of change in the liver (ug/h)
		AL = integ(RL, 38.1156) !amount in liver (ug)
		CL = AL/VL	!concentration in the liver (ug/L)
		CVL = CL/PL	!concentration in the venous blood leaving the liver (ug/L)

		CLiver = AL/ML	!concentration in liver (ug/g)
		
		Abile = kbile*AL

		!Plasma compartment
		RPlas_free = (QR*CVR*Free) + (QK*CVK*Free) + (QL*CVL*Free) - (QC*CA*Free) + RAefflux  !rate of change in the plasma (ug/h) 
		Aplas_free = integ(RPlas_free, 1.21626)	!amount in the plasma (ug)
		CA_free = APlas_free/VPlas		!concentration in plasma (ug)
		CA = CA_free/Free	!concentration of total PFOA in plasma (ug/L)

		!Mass Balance Check
		Atissue = APlas_free + AR + AKb + AFil + APTC + AL + AST + ASI 	!sum of mass in all compartments (ug)
		Aloss = Aurine + Afeces 	!sum of mass lost through urinary and fecal excretion (ug)
		Atotal = Atissue + Aloss 	!total mass; should equal total dose

		END ! DERIVATIVE

		CONSTANT TSTOP = 219000.0 !hours
		TERMT (T .GE. TSTOP, 'checked on communication interval: REACHED TSTOP')

	END ! DYNAMIC
	
	TERMINAL
		! code that is executed once at the end of a simulation run goes here


	END ! TERMINAL

END ! PROGRAM
